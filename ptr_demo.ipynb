{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Tutorial on Penalized Trust Region (PTR) Algorithm - Successive Convex-Optimization"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 1: System Dynamics\n",
    "\n",
    "Assume we have a system with the state $x = [r,v] \\in\\mathbb{R}^4$ characterized by the following nonlinear dynamics:\n",
    "\n",
    "$$ \\begin{align} \\dot{r} &= v \\\\ \\dot{v} &= -D + u \\end{align}$$\n",
    "where $D=\\frac{1}{2}C_D\\lVert v\\rVert ^2\\hat{v}$ is a drag force, $\\hat{v}=\\frac{v}{\\lVert v\\rVert}$, and $u\\in\\mathbb{R}^n$ is the control.\n",
    "\n",
    "Let's try to setup an optimiztaion framework using convex programming theory.\n",
    "\n",
    "First, let's write a function to simulate the dynamics of this system"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# import packages and load helpers\n",
    "using CairoMakie # for plotting\n",
    "using LinearAlgebra # for various linear alebra operations\n",
    "using Convex # for parsing convex optimization problem \n",
    "using ECOS # algorithm for convex solver\n",
    "using OrdinaryDiffEq # for integrating ODEs \n",
    "using DataInterpolations # for creating interpolating objects (i.e. zero-order hold)\n",
    "\n",
    "include(\"helpers.jl\")\n",
    "\n",
    "## Code for dynamics\n",
    "\n",
    "function double_integrator_dynamics!(x_dot, x, p, t)\n",
    "    u, cd = p\n",
    "    \n",
    "    # Unpack state\n",
    "    v = x[3:4]\n",
    "    \n",
    "    # velocity unit vector\n",
    "    v_mag = norm(v)\n",
    "    v_hat = v / v_mag\n",
    "    \n",
    "    # drag force\n",
    "    D = 0.5 * cd * v_mag^2 * v_hat\n",
    "    \n",
    "    # dynamics\n",
    "    x_dot[1:2] .= v\n",
    "    x_dot[3:4] .= -D + u\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 2. Optimization Problem\n",
    "\n",
    "Let's assume we are trying to solve the following optimization problem\n",
    "\n",
    "$$ \\begin{align} \\underset{x(t), u(t)}\\min J(x, u) \\\\ \\mathrm{subject~to}\\quad \\dot{x} &= f(x) \\\\ x(t_0) &= x_0 \\\\ x(t_f) &= x_f \\\\ r_1(t) &\\leq 6 \\end{align}$$\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "where the cost function $J = \\int _0^{t_f} \\lVert u(t) \\rVert _2^2\\mathrm{d}t$"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 3: Convexification Step 1: Linearization\n",
    "\n",
    "Recall for a problem to be convex, the cost function and inequality constraints have to be convex functions, and the equality constraints must be affine in the optimization variables. Because the dynamics are nonlinear, the equality constraint in Equation X is not convex. Therefore, we must approximate the nonlinear dynamics using a first-order Taylor series expansion about some reference ($\\bar{x}(t)$, $\\bar{u}(t)$)\n",
    "$$ \\dot{x}(t) \\approx A(t)(x(t) - \\bar{x}(t)) + B(t)(u(t) - \\bar{u}(t))  = A(t)x(t) + B(t)u(t) + z(t)$$\n",
    "where $z(t) = f(\\bar{x}(t), \\bar{u}(t)) -A(t)\\bar{x}(t) - B(t)\\bar{u}(t)$. \n",
    "\n",
    "The matrices $A$ and $B$ are the partial derivative of the dyamics $\\dot{x}$ with respect to the state $x$ and $control$ u, respectively, evaluated at the reference ($\\bar{x}(t)$, $\\bar{u}(t)$).\n",
    "$$ \\begin{align} A(t) = \\left.\\frac{\\partial f}{\\partial x}\\right|_{\\mathrm{ref}} \\\\ B(t) = \\left. \\frac{\\partial f}{\\partial u}\\right|_{\\mathrm{ref}} \\end{align}$$\n",
    "\n",
    "For our system, we can write these Jacobian matrices analytically as\n",
    "$$ \\frac{\\partial f}{\\partial x} = \\begin{bmatrix} 0 & 0 & 1 & 0 \\\\ 0 & 0 & 0 & 1 \\\\ 0 & 0 & \\dfrac{\\partial \\dot{v}_1}{\\partial v_1} & \\dfrac{\\partial \\dot{v}_1}{\\partial v_2} \\\\ 0 & 0 & \\dfrac{\\partial \\dot{v}_2}{\\partial v_1} & \\dfrac{\\partial \\dot{v}_2}{\\partial v_2} \\end{bmatrix}$$ \n",
    "$$ \\frac{\\partial f}{\\partial u} = \\begin{bmatrix}0 & 0 \\\\ 0 & 0 \\\\ 1 & 0 \\\\ 0 & 1\\end{bmatrix} $$\n",
    "\n",
    "The partial derivatives of the velocity dynamics are written as\n",
    "$$ \\frac{\\partial \\dot{v}_1}{\\partial v_1} = \\frac{1}{2} \\frac{C_d \\left(2 v_1^2 + v_2^2\\right)}{\\lVert v \\rVert} $$\n",
    "$$ \\frac{\\partial \\dot{v}_2}{\\partial v_2} = \\frac{1}{2} \\frac{C_d \\left(v_1^2 + 2v_2^2\\right)}{\\lVert v \\rVert} $$\n",
    "$$ \\frac{\\partial \\dot{v}_1}{\\partial v_2} = \\frac{\\partial \\dot{v}_2}{\\partial v_1} = \\frac{1}{2}C_d v_1 v_2 $$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "## Code for Jacobians\n",
    "\n",
    "function get_AB(t, x_ref, cd)\n",
    "    # Unpack\n",
    "    v = x_ref[3:4]\n",
    "    v1, v2 = v[1], v[2]\n",
    "    v_mag = norm(v)\n",
    "    \n",
    "    # initialze\n",
    "    A_lin = zeros(4, 4)\n",
    "    B_lin = zeros(4, 2)\n",
    "    \n",
    "    # Compute partials\n",
    "    dD1_dv1 = 0.5 * cd * (2 * v1^2 + v2^2) / v_mag\n",
    "    dD1_dv2 = 0.5 * cd * v1 * v2\n",
    "    dD2_dv1 = 0.5 * cd * (v1^2 + 2 * v2^2) / v_mag\n",
    "    dD2_dv2 = 0.5 * cd * v1 * v2\n",
    "    \n",
    "    # Fill out A matrix\n",
    "    A_lin[1, 3] = 1.0\n",
    "    A_lin[2, 4] = 1.0\n",
    "    A_lin[3, 3] = -dD1_dv1\n",
    "    A_lin[3, 4] = -dD1_dv2\n",
    "    A_lin[4, 3] = -dD2_dv1\n",
    "    A_lin[4, 4] = -dD2_dv2 \n",
    "    \n",
    "    # Fill out B matrix\n",
    "    B_lin[3, 1] = 1.0 \n",
    "    B_lin[4, 2] = 1.0 \n",
    "      \n",
    "    return A_lin, B_lin\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Convexification Step 2: Discritization\n",
    "\n",
    "Next, we need to convert the infinite-dimensional optimal control problem into a finite-dimensional parameter optimization problem. Because the system dynamics are now approximated as linear using a Taylor series expansion, we may use the state-transition matrix (STM) to write the following discrete-time, linear dynamics. First define a set of $N$ discrete times such that $t_0 < t_1 < \\ldots < t_{N-1}$\n",
    "$$ x_{k+1} = A_kx_k + B_ku_k + z_k$$\n",
    "where $x_k = x(t = t_k)$. Here, we have imposed a zero-order hold (ZOH) assumption on the control, which means that $u(t) = u_k ~~\\forall t\\in[t_k,t_{k+1})$. The discrete-time matrices above are defined as\n",
    "$$ A_k = \\Phi(t_{k+1},t_k)$$\n",
    "$$ B_k = A_k \\int _{t_k}^{t_{k+1}} \\Phi^{-1}(\\tau, t_k)B(\\tau)\\mathrm{d}\\tau$$\n",
    "$$ h_k = A_k \\int _{t_k}^{t_{k+1}} \\Phi^{-1}(\\tau, t_k)z(\\tau)\\mathrm{d}\\tau$$\n",
    "\n",
    "Here, $\\Phi(t_{k+1}, t_k)$ is the STM from $t=t_k$ to $t=t_{k+1}$ which is computed by integrating the following differential equation\n",
    "$$ \\frac{\\mathrm{d}}{\\mathrm{d}t}\\Phi = A(t)\\Phi(t,t_0)\\quad \\Phi(t_0, t_0)=I$$\n",
    "\n",
    "To each of these matrices is computed by integrating the following ``augmented'' state from $t=t_k$ to $t = t_{k+1}$.\n",
    "\n",
    "$$ \\begin{align} X(t) &= \\begin{bmatrix} \\bar{x}(t) \\\\ \\Phi(t, t_k) \\\\ B(t) \\\\ z(t) \\end{bmatrix} & \\dot{X} &= \\begin{bmatrix} f(\\bar{x}(t), \\bar{u}(t)) \\\\ A(t)\\Phi(t, t_k) \\\\ \\Phi^{-1}(t, t_0)B(t) \\\\ \\Phi^{-1}(t,t_0)z(t) \\end{bmatrix} & X(t_k) = \\begin{bmatrix} \\bar{x}(t_k) \\\\ I \\\\ 0 \\\\ 0 \\end{bmatrix}\\end{align} $$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "## function for augmented state dynamics\n",
    "\n",
    "function stm_derivs(x, p, t)\n",
    "    # unpack parameters\n",
    "    cd, u_ref = p\n",
    "\n",
    "    # Get reference solution\n",
    "    x_ref = x[1:4]\n",
    "    \n",
    "    # Get A, B matrices at reference solution \n",
    "    A, B = get_AB(t, x_ref, cd)\n",
    "\n",
    "    dxdt = zeros(length(x))\n",
    "\n",
    "    # get dynamics for reference\n",
    "    x_ref_dot = zeros(4)\n",
    "    double_integrator_dynamics!(x_ref_dot, x_ref, (u_ref, cd), t)\n",
    "\n",
    "    # x \n",
    "    dxdt[1:4] = x_ref_dot\n",
    "\n",
    "    # STM\n",
    "    STM = reshape(x[5:20], 4, 4)\n",
    "    aux = A * STM\n",
    "    dxdt[5:20] = vec(aux)\n",
    "    \n",
    "    # B matrix\n",
    "    STM_inv = inv(STM)\n",
    "    aux = STM_inv * B\n",
    "    dxdt[21:28] = vec(aux)\n",
    "\n",
    "    # h Vector\n",
    "    aux = x_ref_dot - A*x_ref - B*u_ref\n",
    "    dxdt[29:32] = STM_inv*aux\n",
    "    \n",
    "    return dxdt\n",
    "end\n",
    "\n",
    "## function to compute discsrete matrices at each t_k \n",
    "function get_discrete_matrix(t_ref, x0, u_ref, cd)    \n",
    "    A, B, h = [], [], []\n",
    "\n",
    "    N = length(t_ref)\n",
    "\n",
    "    # initial conditions\n",
    "    STM0 = I(4)\n",
    "    B0 = zeros(4, 2)\n",
    "    h0 = zeros(4)\n",
    "\n",
    "    # initialize x\n",
    "    x_ref = zeros(4, N)\n",
    "    x_ref[:,1] = x0\n",
    "\n",
    "    # loop over each t_k using multiple-shooting approach\n",
    "    for k = 1:N-1\n",
    "        x0_aug = vcat(x_ref[:,k], vec(STM0), vec(B0), h0)\n",
    "        t_span = (t_ref[k], t_ref[k+1])\n",
    "        prob = ODEProblem(stm_derivs, x0_aug, t_span, (cd, u_ref[:,k]))\n",
    "        sol = solve(prob, Tsit5())\n",
    "        x_aug = sol.u[end]\n",
    "        \n",
    "        # extract matrices\n",
    "        x_ref[:,k+1] = x_aug[1:4]\n",
    "        Ak = reshape(x_aug[5:20], 4, 4)\n",
    "        Bk = Ak * reshape(x_aug[21:28], 4, 2)\n",
    "        hk = Ak * x_aug[29:32]\n",
    "\n",
    "        push!(A, Ak)\n",
    "        push!(B, Bk)\n",
    "        push!(h, hk)\n",
    "    end    \n",
    "    return A, B, h\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Convexification Step 3: Trust Regions and Slack Variables (PTR)\n",
    "\n",
    "Due to the linearization and discretization, we introduce the issues of artificial unboundedness and artificial infeasibility. To handle artificial unboundedness, we introduce ``trust regions'' which encourage the optimized solution to stay near the reference trajectory such that the linearized dynamics are valid (enough). To this end, we define\n",
    "\n",
    "$$ \\delta x_k = x_k - \\bar{x}_k$$\n",
    "$$ \\delta u_k = u_k - \\bar{u}_k$$\n",
    "\n",
    "We now impose the following trust region constraint \n",
    "$$ \\lVert \\delta x_k \\rVert _p + \\lVert \\delta u_k \\rVert _p \\leq \\eta_k $$ \n",
    "where $p = \\left\\{1, 2, 2+, \\infty\\right\\}$ is some norm (here $2+$ denotes the 2-norm squared), and $\\eta$ is the trust region radius. Generally, we want to encourage the trust region to be small to drive convergence, so we augment our original cost function with the trust-region radii.\n",
    "$$ \\tilde{J} = J(x,u) + J_{\\mathrm{PTR}}(\\eta)$$\n",
    "where $J_{\\mathrm{PTR}}$ is a trust-region penalization term.\n",
    "\n",
    "Next, to handle artificial infeasibility, we introduce a series of slack variables $\\nu$ and $\\tilde{\\nu}$ to relax the dynamics and inequality constraints \n",
    "$$  x_{k+1} = A_k x_k + B_k u_k + z_k + \\nu_k $$ \n",
    "$$ r_{1,k} \\leq 6 + \\tilde{\\nu}$$\n",
    "\n",
    "Again, we discourage these slack variables by adding them to the cost function\n",
    "$$ \\tilde{J} = J(x,u) + J_{\\mathrm{PTR}}(\\eta) + J_{\\mathrm{slack}}(\\nu, \\tilde{\\nu})$$"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Convexification Step 4: Form Convex Subproblem\n",
    "\n",
    "Now that we have a convex approximation of the original optimal control problem, we can write the ``subproblem'' that will be solved successively.\n",
    "\n",
    "$$ \\begin{align} \\underset{x_k, u_k, \\eta_k, \\nu_k, \\tilde{\\nu}}\\min \\sum _{k=0}^{N-2} u_k^2 + \\lVert \\nu_k\\rVert _1 + \\lVert \\eta_k \\rVert _1 + \\lVert \\tilde{\\nu} \\rVert _1  \\\\ \\mathrm{subject~to}\\quad x_{k+1} &= A_k x_k + B_k u_k + z_k + \\nu_k \\quad \\forall k \\\\ x_{k=0} &= x_0 \\\\ x_{k=N} &= x_f \\\\ r_{1,k} &\\leq 6 + \\tilde{\\nu}_k \\\\ \\lVert \\delta x_k \\rVert + \\lVert \\delta u_k \\rVert &\\leq \\eta_k \\end{align}$$\n",
    "\n",
    "Let's define this subproblem in $\\tt Convex.jl$\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "## Convex subproblem code\n",
    "function solve_subproblem(x_ref, u_ref, x0, xf, N, A, B, h, r1_max)\n",
    "\n",
    "    # define our optimization variables\n",
    "    x = Variable(4, N)\n",
    "    u = Variable(2, N-1)\n",
    "    η = Variable(N)\n",
    "    ν = Variable(4, N)\n",
    "    ν̃ = Variable(N-1)\n",
    "\n",
    "    # initial condition\n",
    "    constraints = Constraint[x[:,1] - x0 == zeros(4)]\n",
    "    # final condition\n",
    "    push!(constraints, x[:,end] - xf == zeros(4))\n",
    "\n",
    "    # dynamics\n",
    "    for k = 1:N-1\n",
    "        push!(constraints, x[:,k+1] == A[k]*x[:,k] + B[k]*u[:,k] + h[k] + ν[:,k])\n",
    "    end\n",
    "\n",
    "    # path constraint and trust regions\n",
    "    for k = 1:N-1\n",
    "        push!(constraints, x[1,k] <= r1_max + ν̃[k])\n",
    "\n",
    "        δx = x[:,k] - x_ref[:,k]\n",
    "        δu = u[:,k] - u_ref[:,k]\n",
    "        push!(constraints, sumsquares(δx) + sumsquares(δu) <= η[k])\n",
    "        push!(constraints, η[k] >= 0)\n",
    "        push!(constraints, ν̃[k] >= 0)\n",
    "    end\n",
    "\n",
    "    # cost function \n",
    "    u_cost = 0\n",
    "    for k = 1:N-1\n",
    "        u_cost += 10*sumsquares(u[:,k])\n",
    "    end\n",
    "    # trust region term\n",
    "    ptr_cost =  1e-1 * norm(η, 2)\n",
    "    # slack variables term \n",
    "    slack_cost = 1000* norm(ν, 1)\n",
    "    buffer_cost =  1000 * norm(ν̃, 1)\n",
    "\n",
    "    cost = u_cost + ptr_cost + slack_cost + buffer_cost\n",
    "\n",
    "    # define problem \n",
    "    problem = minimize(cost, constraints)\n",
    "    solver = ECOS.Optimizer\n",
    "    Convex.solve!(problem, solver; silent = true)\n",
    "\n",
    "    x_opt = evaluate(x)\n",
    "    u_opt = evaluate(u)\n",
    "    \n",
    "    return x_opt, u_opt, evaluate(norm(ν,1))+evaluate(norm(ν̃,1)), evaluate(norm(x-x_ref, 2))\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Successive Solution\n",
    "\n",
    "Now, we must solve our problem successively. This means that the solution of each subproblem becomes the reference for the next subproblem. Let's write some code to do that "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function scp_loop(x0, xf, t_ref, u_ref, N, cd, r1_max)\n",
    "    x_opt, u_opt = [], []\n",
    "\n",
    "    x_ref_all, u_ref_all = [], []\n",
    "\n",
    "    x_ref = initialize_reference(x0, xf, N)\n",
    "    push!(x_ref_all, x_ref)\n",
    "    push!(u_ref_all, u_ref)\n",
    "    total_time = 0\n",
    "    for n = 1:10        \n",
    "        A, B, h = get_discrete_matrix(t_ref, x0, u_ref, cd)\n",
    "\n",
    "        loop_time = @elapsed begin\n",
    "        x_opt, u_opt, residual, dx = solve_subproblem(x_ref, u_ref, x0, xf, N, A, B, h, r1_max)\n",
    "        end\n",
    "        total_time += loop_time\n",
    "        println(\"ITERATION $n: res = $(residual), dx = $dx, elapsed time = $total_time\")\n",
    "\n",
    "        # update control and state references \n",
    "        u_ref = u_opt\n",
    "        x_ref = x_opt\n",
    "        push!(x_ref_all, x_ref)\n",
    "        push!(u_ref_all, u_ref)\n",
    "\n",
    "        # check for convergence\n",
    "        if dx <= 1e-2\n",
    "            break \n",
    "        end\n",
    "    end\n",
    "    return x_opt, u_opt, x_ref_all, u_ref_all\n",
    "end\n",
    "\n",
    "function initialize_reference(x0, xf, N)\n",
    "    x_ref = zeros(4, N)\n",
    "    t = collect(range(0, 1, N))\n",
    "    for (k, tk) in enumerate(t)\n",
    "        x_ref[:,k] = (1-tk)*x0 + tk*xf \n",
    "    end\n",
    "    return x_ref \n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Finally, let's write a wrapper to run all of this"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# wrapper code \n",
    "\n",
    "function run_main()\n",
    "\n",
    "    # initial and final state\n",
    "    x0 = [1, 8, 2, 0]\n",
    "    xf = [1, 2, -1, 0]\n",
    "\n",
    "    # final time \n",
    "    tf = 15\n",
    "\n",
    "    # drag coefficient\n",
    "    cd = 5e-3\n",
    "\n",
    "    # number of nodes in discretization\n",
    "    N = 20\n",
    "\n",
    "    # constraint on x-position\n",
    "    r1_max = 6\n",
    "\n",
    "    # initialize some things\n",
    "    t_ref = collect(range(0, tf, N))\n",
    "    u_ref = zeros(2, N-1)\n",
    "\n",
    "    # run SCP\n",
    "    x_opt, u_opt, x_ref_all, u_ref_all = scp_loop(x0, xf, t_ref, u_ref, N, cd, r1_max)\n",
    "\n",
    "    # assemble control \n",
    "    controller = ConstantInterpolation(u_opt, t_ref[1:end-1], extrapolate=true)\n",
    "\n",
    "    # integrate nonlinear solution\n",
    "    t_nonlinear, x_nonlinear = run_nonlinear(x0, tf, cd, controller)\n",
    "    u_nonlinear = controller(t_nonlinear)\n",
    "\n",
    "    # plot some things\n",
    "    plot_solution(t_ref, x_opt, u_opt, x0, xf, t_nonlinear, x_nonlinear, u_nonlinear, r1_max)\n",
    "    plot_convergence(t_ref, x_ref_all, u_ref_all)\n",
    "\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "## run main code\n",
    "\n",
    "run_main();"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.2",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.2"
  },
  "orig_nbformat": 4
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
